sub init()
    m.top.omniturePageType = "show"

    m.top.observeField("focusedChild", "onFocusChanged")
    
    m.hero = m.top.findNode("hero")
    m.menu = m.top.findNode("menu")

    m.list = m.top.findNode("list")
    m.list.observeField("listItemSelected", "onListItemSelected")
    m.list.initialPositions             = [ 102,  482, 862, 1242, 1622, 2002]
    m.list.forwardIntermediatePositions = [-618, -238, 142,  522,  902, 1282]
    m.list.finalPositions               = [-658, -278, 102,  482,  862, 1242]
    m.list.content = createObject("roSGNode", "ContentNode")

    m.top.setFocus(true)
end sub

sub onFocusChanged()
    if m.top.hasFocus() then
        m.list.setFocus(true)
    end if
end sub

function onKeyEvent(key as string, press as boolean) as boolean
    if press then
'        if key = "down" then
'            if m.rows.isInFocusChain() then
'                m.list.scrollToChild = m.list.pendingChildScrolled + 1
'                m.hero.visible = false
'                return true
'            end if
'        else if key = "up" then
'            if m.rows.isInFocusChain() then
'                if m.list.childScrolled > 0 then
'                    m.list.scrollToChild = m.list.pendingChildScrolled - 1
'                    if m.list.scrollToChild = 0 then
'                        m.hero.visible = true
'                    end if
'                else
'                    m.rowsScrollOutAnimation.control = "start"
'                    m.hero.setFocus(true)
'                end if
'                return true
'            end if
'        else if key = "options" then
'        end if
    end if
    return false
end function

sub onShowChanged()
    if m.show = invalid or not m.show.isSameNode(m.top.show) then
        m.show = m.top.show

        pageName = "/shows/" + lCase(m.show.title)
        m.top.omnitureName = pageName
        trackScreenView()

        m.hero.uri = m.show.heroImageUrl
        m.list.content = m.show
        for each section in m.show.sections
            if section.hasField("loadIndex") then
                section.loadIndex = 0
            end if
        next
    end if
end sub

sub onShowIDChanged()
    m.loadTask = createObject("roSGNode", "ShowScreenTask")
    m.loadTask.observeField("show", "onShowLoaded")
    m.loadTask.showID = m.top.showID
    m.loadTask.control = "run"
end sub

sub onShowLoaded()
    m.top.show = m.loadTask.show
    m.loadTask = invalid
end sub

sub onListItemSelected(nodeEvent as object)
    list = nodeEvent.getRoSGNode()
    if list <> invalid then
        selected = nodeEvent.getData()
        row = list.content.sections[selected[0]]
        index = selected[1]
        if row <> invalid then
            if row.subtype() = "Show" then
                trackScreenAction("trackPodSelect", getOmnitureData(row, 0, lCase(row.dynamicPlayEpisode.title)))
                m.top.itemSelected = row.dynamicPlayEpisode.episode
            else
                item = row.getChild(index)
                if item <> invalid then
                    trackScreenAction("trackPodSelect", getOmnitureData(row, index, iif(isAuthenticated(m.global), "pay", "free")))
                    m.top.itemSelected = item
                end if
            end if
        end if
    end if
end sub
