sub init()
    m.sprite = m.top.findNode("sprite")
    m.sprite.observeField("loadStatus", "onSpriteLoadStatusChanged")
    m.frameTimer = m.top.findNode("frameTimer")
    m.frameTimer.observeField("fire", "onFrameTimerFired")
    
    m.top.observeField("visible", "onVisibilityChanged")

    m.clippingRects = []
    m.frameIndex = 0
end sub

sub updateLayout()
    if m.top.width > 0 and m.top.height > 0 and m.top.horizontalSlices > 0 and m.top.verticalSlices > 0 then
        onSpriteLoadStatusChanged()
    end if
end sub

sub onVisibilityChanged()
    if m.top.visible then
        m.frameTimer.control = "start"
    else
        m.frameTimer.control = "stop"
    end if
end sub

sub onSpriteLoadStatusChanged()
    if m.sprite.loadStatus = "ready" then
        if m.top.width > 0 and m.top.height > 0 and m.top.horizontalSlices > 0 and m.top.verticalSlices > 0 then
            if m.sprite.bitmapWidth > 0 and m.sprite.bitmapHeight > 0 then
                m.clippingRects = []
                spriteWidth = m.sprite.bitmapWidth \ m.top.horizontalSlices
                spriteHeight = m.sprite.bitmapHeight \ m.top.verticalSlices
                m.sprite.scale = [m.top.width / spriteWidth, m.top.height / spriteHeight]
                for y = 0 to m.top.verticalSlices - 1
                    for x = 0 to m.top.horizontalSlices - 1
                        m.clippingRects.push({
                            x: x * spriteWidth
                            y: y * spriteHeight
                            w: spriteWidth
                            h: spriteHeight
                        })
                    next
                next
                m.frameTimer.control = "start"
            end if
        end if
    end if
end sub

sub onFrameTimerFired()
    if not m.clippingRects.isEmpty() then
        clippingRect = m.clippingRects[m.frameIndex]
        if clippingRect <> invalid then
            m.sprite.clippingRect = [clippingRect.x, clippingRect.y, clippingRect.w, clippingRect.h]
            m.sprite.translation = [-clippingRect.x * m.sprite.scale[0], -clippingRect.y * m.sprite.scale[1]]
        end if
        m.frameIndex = m.frameIndex + 1
        if m.frameIndex >= m.clippingRects.count() then
            m.frameIndex = 0
        end if
    end if
end sub